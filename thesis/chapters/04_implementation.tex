\chapter{Implementation}
\label{chap:implementation}

In this chapter, we present an implementation of the mentioned type inference algorithm in Haskell. First, we define object language grammar in Labelled BNF and generate front-end of the type checker. Second, we generate generic abstract syntax with Free Foil. Then, we introduce necessary data types and explain key steps in the algorithm implementation. Finally, we talk about testing and related difficulties.

\section{Grammar and Front-end}

The object language, for which the type checking is to be implemented is similar to the simply typed lambda-calculus but extended with let polymorphism, boolean and natural number literals. The grammar is defined in Labelled BNF (LBNF), and is divided into two parts: expressions and types.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\subsection{Expressions Grammar}

The LBNF grammar for expressions is shown in Figure~\ref{fig:lbnf-terms}. We define 7 grammar rules for expressions:

\begin{itemize}
  \item \texttt{EVar} — variable identifier.
  \item \texttt{ETrue} and \texttt{EFalse} — boolean literals.
  \item \texttt{ENat} — natural number literal.
  \item \texttt{EApp} — function application. Note that subexpression on the right has a higher precedence level, since function application is left associative.
  \item \texttt{EAbs} — lambda abstraction, which binds a variable to be used in the nested expression.
  \item \texttt{ELet} — let-binding, which is similar to \texttt{EAbs} but has one more (not scoped) subexpression.
\end{itemize}

\texttt{Pattern} and \texttt{ScopedExp} rules are essentially wrappers around \texttt{Ident} and \texttt{Exp1} respectively, which are introduced to clearly identify a binder and corresponding scoped expressions where the bound pattern can occur. These rules are crucial for the scope-safe abstract syntax generation, which is described in the next section.

\begin{figure}[H]
  \begin{minted}[frame=single,fontsize=\small,escapeinside=@@]{text}
EVar.    Exp3 ::= Ident ;
ETrue.   Exp3 ::= "true" ;
EFalse.  Exp3 ::= "false" ;
ENat.    Exp3 ::= Integer ;
EApp.    Exp2 ::= Exp2 Exp3 ;
EAbs.    Exp1 ::= "@$\lambda$@" Pattern "." ScopedExp ;
ELet.    Exp1 ::= "let" Pattern "=" Exp1 "in" ScopedExp ;
coercions Exp 3 ;

PatternVar. Pattern ::= Ident ;
ScopedExp. ScopedExp ::= Exp1 ;
  \end{minted}
  \caption[LBNF grammar of the object language expressions]{Labelled BNF grammar of the object language expressions. \texttt{Ident} and \texttt{Integer} are predifined basic types in BNFC. The \texttt{coercions} is a macro that automatically generates semantic "no-op" coercion rules that define the precedence of the operators.}
  \label{fig:lbnf-terms}
\end{figure}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\subsection{Types Grammar}

Figure~\ref{fig:lbnf-types} shows the LBNF grammar for types, which defines 6 grammar rules:

\begin{itemize}
  \item \texttt{TUVar} — free, or unification variable.
  \item \texttt{TBool} — boolean type.
  \item \texttt{TNat} — natural number type.
  \item \texttt{TVar} — type variable.
  \item \texttt{TArrow} — function type. Unlike function expressions, function types are right associative, therefore, rule to the left of the arrow has higher precedence.
  \item \texttt{TForAll} — "forall"-quantified type that binds a type variable in the inner type expression.
\end{itemize}

To distinguish between free and bound variables we prefix unification variable identifiers with "?" by introducing a new \texttt{UVarIdent} lexical type. And, similarly to term expressions, we also define separate \texttt{TypePattern} and \texttt{ScopedType} rules to further generate the scope-safe abstract syntax for types. In case of types, the only rule that binds a (type) variable and has a scoped subexpression is \texttt{TForAll}.

\begin{figure}[H]
  \begin{minted}[frame=single,fontsize=\small,escapeinside=@@]{text}
token UVarIdent ('?' letter (letter | digit | '_')*) ;

TUVar.   Type2 ::= UVarIdent ;
TNat.    Type2 ::= "Nat" ;
TBool.   Type2 ::= "Bool" ;
TVar.    Type2 ::= Ident ;
TArrow.  Type1 ::= Type2 "->" Type1 ;
TForAll. Type  ::= "forall" TypePattern "." ScopedType ;
coercions Type 2 ;

TPatternVar. TypePattern ::= Ident ;
ScopedType. ScopedType ::= Type1 ;
  \end{minted}
  \caption[LBNF grammar of the object language types]{Labelled BNF grammar of the object language types. \texttt{UVarIdent} is a custom lexical type introduced to distinguish unification (free) variables from bound variables, which use the predifined basic type \texttt{Ident}. The \texttt{coercions} is a macro that automatically generates semantic "no-op" coercion rules that define the precedence of the operators.}
  \label{fig:lbnf-types}
\end{figure}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\subsection{Code Generation}

Having defined the language grammar, we are ready to generate Haskell code for type-checker front-end, using code generation tools mentioned in the Chapter~\ref{chap:lr}. After installing all necessary tools, and running shell commands from Figure~\ref{fig:code-gen-cli}, we get:

\begin{enumerate}
  \item Modules with abstract syntax data types (\texttt{Abs.hs}), pretty-printing functions (\texttt{Print.hs}), and lexer and parser specifications (\texttt{Lex.x} and \texttt{Par.y}) are generated by BNFC~\cite{BNFC} from the grammar.
  \item Lexical analyzer (\texttt{Lex.hs}) is generated by Alex\footnote{Alex - A Lexical Analyser Generator for Haskell, \url{https://github.com/haskell/alex}} from the lexer specification.
  \item Parser (\texttt{Par.hs}) is generated by Happy\footnote{Happy - The Parser Generator for Haskell, \url{https://github.com/haskell/happy}} from the parser specification.
\end{enumerate}

\begin{figure}[H]
  \begin{minted}[frame=single]{bash}
bnfc --haskell -d \
  -p FreeFoilTypecheck.HindleyMilner \
  --generic \
  -o src \
  grammar/Parser.cf
alex src/FreeFoilTypecheck/HindleyMilner/Parser/Lex.x
happy src/FreeFoilTypecheck/HindleyMilner/Parser/Par.y
  \end{minted}
  \caption{Shell commands for generating type checker front-end}
  \label{fig:code-gen-cli}
\end{figure}

\begin{figure}[H]
\begin{minted}[frame=single,fontsize=\small]{haskell}
newtype Ident = Ident String

data Pattern = PatternVar Ident
data Exp
  = EVar Ident
  | ETrue
  | EFalse
  | ENat Integer
  | EApp Exp Exp
  | EAbs Pattern ScopedExp
  | ELet Pattern Exp ScopedExp
data ScopedExp = ScopedExp Exp

newtype UVarIdent = UVarIdent String
data TypePattern = TPatternVar Ident
data Type
  = TUVar UVarIdent
  | TNat
  | TBool
  | TVar Ident
  | TArrow Type Type
  | TForAll TypePattern ScopedType
data ScopedType = ScopedType Type
\end{minted}
  \caption{Abstract syntax data types generated by BNFC}
  \label{fig:ast-types-bnfs}
\end{figure}

From the abstract syntax generated by BNFC shown in Figure~\ref{fig:ast-types-bnfs}, we now are able to generate the generic and scope-safe version of it via Free Foil.

\section{Generating Scope-Safe SOAS}

The Free Foil framework is accessible in Haskell via the free-foil package~\cite{free-foil}. To generate scope-safe second-order abstract syntax (SOAS) for the syntactical constructs of our language, Free Foil requires the first-order syntax to be organized in four types~\cite{FreeFoil}: \textit{variable identifiers}, \textit{patterns} (binders), \textit{scoped terms}, and (unscoped) \textit{terms}. This is exactly what we have generated with BNFC (Figure~\ref{fig:ast-types-bnfs}). In total, we supply free-foil with 7 types to generate SOAS, since we use expressions and types separately:

\begin{enumerate}
  \item \texttt{Ident} — type of \textit{variable identifier} in both expressions and types.
  \item \texttt{Pattern} — type of \textit{pattern} in expressions.
  \item \texttt{ScopedExp} — type of \textit{scoped terms} in expressions.
  \item \texttt{Exp} — type of \textit{terms} in expressions.
  \item \texttt{TypePattern} — type of \textit{patterns} in types.
  \item \texttt{ScopedType} — type of \textit{scoped terms} in types.
  \item \texttt{Type} — type of \textit{terms} in types.
\end{enumerate}

Figure~\ref{fig:soas-gen-types} presents the generation of signature for type terms, necessary class instances, and other helpers to be used later, as well as the corresponding actually generated code. Code-generation for language expressions is omitted, as it is identical.

\begin{figure}[H]
\begin{minted}[frame=single,fontsize=\small]{haskell}
-- Signature.
mkSignature ''Raw.Type ''Raw.Ident ''Raw.ScopedType ''Raw.TypePattern
deriveZipMatch ''TypeSig
deriveBifunctor ''TypeSig
deriveBifoldable ''TypeSig
deriveBitraversable ''TypeSig

-- Scope-safe type patterns.
mkFoilPattern ''Raw.Ident ''Raw.TypePattern

--------------------------------------------------------------------------

-- Generated by mkSignature TH function.
data TypeSig scope term
  = TUVarSig UVarIdent
  | TNatSig
  | TBoolSig
  | TArrowSig term term
  | TForAllSig scope

-- Generated by derive* TH functions.
instance ZipMatch TypeSig where ...
instance Functor (TypeSig scope) where ...
instance Bifunctor TypeSig where ...
instance Foldable (TypeSig scope) where ...
instance Bifoldable TypeSig where ...
instance Traversable (TypeSig scope) where ...
instance Bitraversable TypeSig where ...

-- Generated by mkFoilPattern TH funciton.
data FoilPattern (n :: S) (l :: S) where
  FoilPatternVar
    :: forall (n :: S) (l :: S). NameBinder n l
    -> FoilPattern n l
\end{minted}
  \caption[Generating SOAS (1): signature of type terms]{SOAS generation with free-foil's Template Haskell functions, and corresponding generated code.}
  \label{fig:soas-gen-types}
\end{figure}

We also generate pattern synonyms\footnote{Pattern synonyms, enabled by the GHC "PatternSynonyms" extension, allow more convenient and readable pattern matching on complex data types.} for language expressions with free-foil's \texttt{mkPatternSynonyms} TH function (Figure~\ref{fig:soas-gen-exprs}), which will be used later when pattern-matching on language expressions during type inference. It is clear to see the structure of language expressions from the generated code, and that lambda-abstractions and let-bindings, for example, contain scoped subexpression i.e., expression bound by a binder.

\begin{figure}[H]
\begin{minted}[frame=single,fontsize=\small]{haskell}
mkPatternSynonyms ''ExpSig

-- Generated by mkPatternSynonyms TH function.
pattern ETrue        = FreeFoil.Node (ETrueSig) 
pattern EFalse       = FreeFoil.Node (EFalseSig)
pattern ENat n       = FreeFoil.Node (ENatSig n)
pattern EApp e1 e2   = FreeFoil.Node (EAppSig e1 e2)
pattern EAbs b e     = FreeFoil.Node (EAbsSig (FreeFoil.ScopedAST b e))
pattern ELet e1 b e2 = FreeFoil.Node (ELetSig e1 (FreeFoil.ScopedAST b e2))
\end{minted}
  \caption[Generating SOAS (2): pattern synonyms for expressions]{Pattern synonyms for language expressions generated with free-foil.}
  \label{fig:soas-gen-exprs}
\end{figure}

Finally, we declare synonyms for the generated SOAS data types for convenience (Figure~\ref{fig:soas-type-synonyms}). \texttt{Type n} is a type synonym with phantom parameter \texttt{n}, which represents a single AST node in scope \texttt{n}, and is used throughout the implementation. \texttt{Type'} is a synonym for a type term in empty scope i.e., in the scope where all variables (if any) are bound. Synonyms for expressions are similar.

\begin{figure}[H]
\begin{minted}[frame=single,fontsize=\small]{haskell}
type Type n = AST FoilTypePattern TypeSig n
type Type' = Type Foil.VoidS

type Exp n = AST FoilPattern ExpSig n
type Exp' = Exp Foil.VoidS
\end{minted}
  \caption[Generating SOAS (3): type synonyms]{Type synonyms for generated SOAS.}
  \label{fig:soas-type-synonyms}
\end{figure}

\section{Typechecker}

\subsection{Typing Context}

As with algorithm $\mathcal{L}$ in the previous chapter, we begin implementation with the typing context. \texttt{TypingContext} (Figure~\ref{fig:impl-typing-context}) is a record, where each field corresponds to a component of $\mathcal{T}$:

\begin{description}
  \item[\texttt{tcConstraints}] corresponds to $C$, a list of the constraints. We declare a new type (\texttt{Constraint n}), which is a pair of types in scope \texttt{n}.
  \item[\texttt{tcSubst}] corresponds to $\sigma$, a substitution that maps unification variables to types. We declare a new type (\texttt{Subst n}), which wraps the \texttt{Data.Map} from the \texttt{containers} package.
  \item[\texttt{tcEnv}] corresponds to $\Gamma$, a mapping from expression variables to their types. We declare a new type (\texttt{TypingEnv n}) which wraps the free-foil's \texttt{NameMap} data type.
  \item[\texttt{tcLevelMap}] corresponds to $\mathcal{L}$, a mapping from unification variables to their levels. We declare a synonym \texttt{IdentLevelMap} for \texttt{HashMap} from the \texttt{containers} package.
  \item[\texttt{tcLevel}] corresponds to $\ell$, which is just \texttt{Int} meaning the current level.
\end{description}

We also add an integer field \texttt{tcFreshId} to the context, which is used to generate fresh names for unification variables.

New types \texttt{Constraint n}, \texttt{Subst n}, and \texttt{TypingEnv n} may contain scoped terms and, therefore, have to specify the scope that these terms belong to. In the implementation, we separate free and bound variables: for free (or unification) variables we use the \texttt{TUVar} constructor, while for bound variables we use free-foil's \texttt{FreeFoil.Var}. Consequently, we can be sure that all types stored in the context are in empty scope (i.e., all free-foil's variables inside types are bound), so we use empty-scoped synonyms: \texttt{Constraint'}, \texttt{Subst'}, and \texttt{TypingEnv'}.

\begin{figure}[H]
\begin{minted}[frame=single,fontsize=\small]{haskell}
data TypingContext n = TypingContext
  { tcConstraints :: [Constraint'],
    tcSubst :: Subst',
    tcEnv :: TypingEnv' n,
    tcLevelMap :: IdentLevelMap,
    tcLevel :: Int,
    tcFreshId :: Int
  }

newtype Constraint n = Constraint (Type n, Type n)
type Constraint' = Constraint Foil.VoidS

newtype Subst n = Subst (Map.Map Raw.UVarIdent (Type n))
type Subst' = Subst Foil.VoidS

newtype TypingEnv n tn = TypingEnv (Foil.NameMap n (Type tn))
type TypingEnv' n = TypingEnv n Foil.VoidS

type IdentLevelMap = HashMap.HashMap Raw.UVarIdent Int
\end{minted}
  \caption[\texttt{TypingContext} and its components]{Declaration of \texttt{TypingContext} and its components}
  \label{fig:impl-typing-context}
\end{figure}

For the implementation, we deliberately chose to use simple data types such as strings, integers, and hashmap, rather than relying on more sophisticated approaches. This decision was made to keep the code self-contained and straightforward, prioritizing ease of understanding over performance.

\subsection{\texttt{Typed} Type-Class}

Some parts of the inference algorithm require three operations on the type terms to be defined. First is application of a substitution to a type. Second is extraction of free variables from a type. Third is checking whether a free variable occurs in type. Moreover, not only the type terms should support these operations, but also the typing environment ($\Gamma$) and constraints inside of the constraint set ($C$). We generalize these operations (which we named \texttt{applySubst}, \texttt{freeVars}, and \texttt{hasFreeVar}) to the \texttt{Typed} type-class and implement them for the following types:

\begin{itemize}
  \item \texttt{Type n} (which is a synonym for \texttt{AST FoilTypePattern TypeSig});
  \item \texttt{TypingEnv} and \texttt{Constraint} that contain types.
\end{itemize}

Declaration of the \texttt{Typed} and implementation of its instances are shown on the Figure~\ref{fig:typed-class}. Thanks to generic nature of our AST representation, implemention of the mentioned operations is very concise. Since we have generated \texttt{Foldable} and \texttt{Functor} instances for the \texttt{Type n} (Figure~\ref{fig:soas-gen-types}), we use generic \texttt{foldr} and \texttt{fmap} functions to recursively apply substitution (defined later) and extract free variables from a term.

\begin{figure}[H]
  \begin{minted}[frame=single,fontsize=\small]{haskell}
class Typed a where
  applySubst :: (Foil.Distinct n) => Subst n -> a n -> a n

  freeVars :: a n -> Set.Set Raw.UVarIdent

  hasFreeVar :: Raw.UVarIdent -> a n -> Bool
  hasFreeVar i t = i `Set.member` freeVars t

instance Typed (FreeFoil.AST FoilTypePattern TypeSig) where
  applySubst (Subst s) t = foldr applySubstToType t (Map.toList s)

  freeVars (TUVar ident) = Set.singleton ident
  freeVars (FreeFoil.Var _) = Set.empty
  freeVars (FreeFoil.Node node) = Set.unions $ fmap freeVars node

instance Typed Constraint where
  applySubst s (Constraint (t1, t2)) =
    Constraint (applySubst s t1, applySubst s t2)

  freeVars (Constraint (t1, t2)) = Set.union (freeVars t1) (freeVars t2)

instance Typed (TypingEnv n) where
  applySubst s (TypingEnv env) = TypingEnv (fmap (applySubst s) env)

  freeVars (TypingEnv env) = Set.unions $ freeVars <$> env  
  \end{minted}
  \caption[\texttt{Typed} type-class]{Declaration of \texttt{Typed} type-class and its instances for \texttt{Term n}, \texttt{Constraint}, and \texttt{TypingEnv}.}
  \label{fig:typed-class}
\end{figure}

In fact, the presented implementation is independent from the actual type terms that we defined in the beginning, therefore, extending the type system with new terms would not require any changes of this part of the code.

\subsection{Substitution}

Type inference requires two operations to be implemented for substitutions: composition and application (Figure~\ref{fig:impl-substitution}).

\texttt{composeSubst} function takes two substitutions and returns their composition. Remember that \texttt{Typed} instance is defined for \texttt{Subst n}, therefore, we can use \texttt{applySubst} to apply one substitution to another.

\texttt{applySubstToType} recursively traverses a type, replacing all occurrences of a given unification variable with the corresponding type. Since \texttt{Bifunctor} is defined for type terms, we use \texttt{bimap} to recursively apply substitution for both scoped and unscoped AST nodes.

Notice the benefits of intrinsic scoping and generality:

\begin{enumerate}
  \item To transform an AST node, we are forced to tell the compiler how to do it both for scoped and unscoped versions. For the scoped terms, we need to ensure that a binder does not capture free variables inside the body, and then reinterpret body with another scope (which is called \emph{sinking}). \texttt{Distinct} type-class, which ensures that all names inside a scope are distinct, is required for sinking, therefore, it is propagated to \texttt{applySubstToType}, \texttt{applySubst}, and \texttt{composeSubst}. Although this approach may appear to complicate the process, it actually \textbf{prevents subtle bugs related to name capture}.
  \item We do not see terms that we defined before in Figure~\ref{fig:impl-substitution}, because the free-foil's generic \texttt{Node} abstracts them. Again, adding new terms or modifying existing, \textbf{will not require any changes} in the discussed code snippet.
\end{enumerate}

\begin{figure}[H]
\begin{minted}[frame=single,fontsize=\small]{haskell}
composeSubst
  :: (Foil.Distinct n)
  => Subst n
  -> Subst n
  -> Subst n
composeSubst (Subst m1) s2@(Subst m2) =
  Subst (Map.map (applySubst s2) m1 `Map.union` m2)

applySubstToType
  :: (Foil.Distinct n)
  => (Raw.UVarIdent, Type n)
  -> Type n
  -> Type n
applySubstToType (ident, type_) (TUVar x)
  | ident == x = type_
  | otherwise = TUVar x
applySubstToType _ (FreeFoil.Var x) = FreeFoil.Var x
applySubstToType subst (FreeFoil.Node node) = FreeFoil.Node
  (bimap (applySubstToScopedType subst) (applySubstToType subst) node)

applySubstToScopedType
  :: (Foil.Distinct n)
  => (Raw.UVarIdent, Type n)
  -> FreeFoil.ScopedAST FoilTypePattern TypeSig n
  -> FreeFoil.ScopedAST FoilTypePattern TypeSig n
applySubstToScopedType subst (FreeFoil.ScopedAST binder body) =
  case (Foil.assertExt binder, Foil.assertDistinct binder) of
    (Foil.Ext, Foil.Distinct) -> FreeFoil.ScopedAST
      binder (applySubstToType (fmap Foil.sink subst) body)
\end{minted}
  \caption[Substitution composition and application]{Implementation of substitution composition (\texttt{composeSubst}) and application (\texttt{applySubstToType} and \texttt{applySubstToScopedType}).}
  \label{fig:impl-substitution}
\end{figure}

\subsection{Unification}

\texttt{unifyConstraint} (Figure~\ref{fig:unification-impl}) implements a standard unification algorithm similar to Robinson~\cite{Robinson1965}, which is used in the classical algorithm $\mathcal{W}$, with two peculiarities.

First, we rely on free-foil's \texttt{zipMatch} function that unifies two arbitary AST nodes, which are then converted to constraints to further unify recursively with \texttt{unifyConstraints}. Since \texttt{zipMatch} is generic, unification works universally for all term types in AST.

Second, as our implementation of generalization relies on levels, level assigned to a type variable may be updated during unification~\cite{Kiselyov2022_OCamplTypeChecker}, therefore, we need to update \texttt{IdentLevelMap}.

\begin{figure}[H]
\begin{minted}[frame=single,fontsize=\small]{haskell}
unifyConstraint
  :: IdentLevelMap -> Constraint'
  -> Either String (Subst', IdentLevelMap)
unifyConstraint levelsMap (Constraint constr) =
  case constr of
    (TUVar x, r) -> case r of
      TUVar y
        | x == y -> Right (idSubst, levelsMap)
        | otherwise -> unifyWithUVar levelsMap x r
      _ -> unifyWithUVar levelsMap x r
    (l, TUVar x) -> unifyWithUVar levelsMap x l
    (FreeFoil.Node l, FreeFoil.Node r) ->
      case FreeFoil.zipMatch l r of
        Nothing -> Left ("cannot unify " ++ show constr)
        Just lr -> unifyConstraints levelsMap (Constraint <$> F.toList lr)
    (lhs, rhs) -> Left ("cannot unify " ++ show lhs ++ show rhs)

unifyWithUVar
  :: IdentLevelMap -> Raw.UVarIdent -> Type'
  -> Either String (Subst', IdentLevelMap)
unifyWithUVar levelsMap x type_
  | hasFreeVar x type_ = Left "occurs check failed"
  | otherwise = case HashMap.lookup x levelsMap of
      Nothing -> Left "type variable w/o level"
      Just level ->
        let vars = Set.toList (freeVars type_)
            withXLevels = HashMap.fromList [(var, level) | var <- vars]
            updatedLevels = HashMap.unionWith min levelsMap withXLevels
          in Right (singleSubst x type_, updatedLevels)

unifyConstraints
  :: IdentLevelMap -> [Constraint']
  -> Either String (Subst', IdentLevelMap)        
\end{minted}
  \caption[Implementation of \texttt{unifyConstraint}]{Implementation of \texttt{unifyConstraint} and \texttt{unifyWithUVar} functions.}
  \label{fig:unification-impl}
\end{figure}

\texttt{unifyConstraint} is split into two logical parts:

\begin{enumerate}
  \item If at least one side of the constraint is a unification variable, we either return the identity substitution and unchanged level map if both sides are the same variable, or otherwise delegate to \texttt{unifyWithUVar}. The \texttt{unifyWithUVar} helper performs the "occurs check", produces a single-binding substitution, and updates \texttt{IdentLevelMap} so that all unification variables of the unifiable type are assigned the lowest.
  \item If both sides are nodes, we call \texttt{zipMatch}. If \texttt{Nothing} is returned, it means that nodes do not match and unification fails. Otherwise, we map the result to a list of constraints, and recursively unify them with \texttt{unifyConstraints}.
\end{enumerate}

If neither of the cases applies, the two types are incompatible and unification fails.

\subsection{\texttt{TypeInferencer} Monad}

Looking at the definition of $\mathcal{L}$ (Figure~\ref{fig:algorithm-L}), we can notice that the typing context is passed to each recursive call, and then overwritten by the returned one. This pattern resembles a monad.

We define a new \texttt{TypeInferencer} type and implement \texttt{Monad} instance for it (Figure~\ref{fig:TypeInferencer}). It is a record with a single field \texttt{runTI}, which is a function that takes a typing context, and returns either an error message or a pair of a result with a new typing context. By introducing just a few lines of code, we significantly improve the structure and readability of the program.

We also declare utility-functions that operate on the \texttt{TypeInferencer} monad (here we only declare the types, implementation can be found in Appendix~\ref{chap:appendix}):

\begin{enumerate}
  \item \texttt{freshUVar\_} — returns a fresh unification variable, automatically recording its level to \texttt{tcLevelMap}, and incrementing the fresh identifier counter.
  \item \texttt{addConstraints} — simply adds new constraints to the typing context.
  \item \texttt{enterScope} — performs a monadic operation in a context where the \texttt{TypingEnv} is extended with a new binder and its associated type; it automatically adds and removes the binder, ensuring scope-safety.
  \item \texttt{enterLevel} — performs a monadic operation while temporarily incrementing the \texttt{tcLevel} field in the context, and then restores it to its previous value.
  \item \texttt{generalize} — corresponds to the $\mathrm{gen}$ (Figure~\ref{fig:algorithm-L-types}). It generalizes a type, quantifying all type variables accounting for levels, implicitly taking information from the context.
  \item \texttt{specialize\_} — corresponds to the $\mathrm{spec}$ (Figure~\ref{fig:algorithm-L-types}). It specializes a type, replacing all quantified type variables with fresh unification variables.
  \item \texttt{unify} — implicitly resolves all constraints in the typing context, applies the resulting substitution to the typing environment, and updates the context.
\end{enumerate}

\begin{figure}[H]
\begin{minted}[frame=single,fontsize=\small]{haskell}
newtype TypeInferencer n a = TypeInferencer
  { runTI :: TypingContext n -> Either String (a, TypingContext n) }

instance Monad (TypeInferencer n) where
  TypeInferencer g >>= f = TypeInferencer $ \ctx -> do
    (x, ctx') <- g ctx
    runTI (f x) ctx'

freshUVar_ :: TypeInferencer n Type'
addConstraints :: [(Type', Type')] -> TypeInferencer n ()
enterScope
  :: Foil.NameBinder n l -> Type' -> TypeInferencer l a
  -> TypeInferencer n a
enterLevel :: TypeInferencer n a -> TypeInferencer n a
generalize :: Type' -> TypeInferencer n Type'
specialize_ :: Type' -> TypeInferencer n Type'
unify :: TypeInferencer n ()
\end{minted}
  \caption[\texttt{TypeInferencer} monad]{\texttt{TypeInferencer} monad and type declarations of utility functions.}
  \label{fig:TypeInferencer}
\end{figure}  

\subsection{Inference}

Having declared all the necessary data types and functions, we are ready to implement the \texttt{inferType} function that takes an expression and returns monadic operation that performs type inference (Figure~\ref{fig:inferType}). It recursively infers a type of a given expression by pattern matching the signature using the generated pattern synonyms (Figure~\ref{fig:soas-gen-exprs}). The inference for each expression type in \texttt{inferType} exactly corresponds to the cases defined in the algorithm $\mathcal{L}$ (see Figure~\ref{fig:algorithm-L}).

\begin{figure}[H]
  \begin{minted}[frame=single,fontsize=\small]{haskell}
inferType :: Exp n -> TypeInferencer n Type'
inferType ETrue = return TBool
inferType EFalse = return TBool
inferType (ENat _) = return TNat
inferType (FreeFoil.Var x) = do
  TypingEnv env <- gets tcEnv
  specialize_ (Foil.lookupName x env)  
inferType (EAbs (FoilPatternVar x) eBody) = do
  tParam <- freshUVar_
  tBody <- enterScope x tParam $ inferType eBody
  return (TArrow tParam tBody)
inferType (EApp eAbs eArg) = do
  tAbs <- inferType eAbs
  tArg <- inferType eArg
  tRes <- freshUVar_
  addConstraints [(tAbs, TArrow tArg tRes)]
  return tRes
inferType (ELet eBound (FoilPatternVar x) eInner) = do
  tBound <- enterLevel (inferType eBound)
  unify
  subst <- gets tcSubst
  tBound' <- generalize (applySubst subst tBound)
  enterScope x tBound' (inferType eInner)
\end{minted}
  \caption[Implementation of \texttt{inferType}]{Implementation of the \texttt{inferType} function.}
  \label{fig:inferType}
\end{figure}

Result returned by the \texttt{inferType} contains both the inferred type and typing context, however, the context may still contain unresolved constraints. To get the final most general type of expression, all constraints accumulated in the typing context must be resolved (i.e., unified), the resulting substitution should be applied to the type, and the type should be generalized. \texttt{inferTypeClosed} (Figure~\ref{fig:inferTypeClosed}) supplies \texttt{inferType} with the initial typing context, runs the necessary monadic operations, and returns the final most general type of expression.

\begin{figure}[H]
\begin{minted}[frame=single,fontsize=\small,linenos]{haskell}
inferTypeClosed :: Exp Foil.VoidS -> Either String Type'
inferTypeClosed expr = do
  (t, ctx) <- runTI (inferType expr) initialTypingContext
  (_, ctx') <- runTI unify ctx
  let t' = applySubst (tcSubst ctx') t
  (tGen, _) <- runTI (generalize t') (ctx' {tcLevel = 0})
  return tGen

initialTypingContext :: TypingContext Foil.VoidS
initialTypingContext = TypingContext
  { tcConstraints = [],
    tcSubst = Subst Map.empty,
    tcEnv = TypingEnv Foil.emptyNameMap,
    tcFreshId = 1,
    tcLevelMap = HashMap.empty,
    tcLevel = 1
  }
\end{minted}
  \caption[Implementation of \texttt{inferTypeClosed}]{Implementation of the \texttt{inferTypeClosed} function, which takes an expression, and runs \texttt{inferType} with the initial typing context performing all necessary post-processing steps. On line 6, we update the level to 0 to make the \texttt{generalize} operation quantify all free variables.}
  \label{fig:inferTypeClosed}
\end{figure}

\section{Testing}

A test suite consisting of 19 ill-typed and 33 well-typed programs has been developed when implementing the type inference. This helped to find bugs during development and, to some extent, verify the correctness of the implemented algorithm.

Test cases with ill-typed programs check that the inference algorithm indeed fails to infer the type of such programs For an ill-typed program, the test case passes only if \texttt{inferTypeClosed} returns an error, for example, when unification of \texttt{TBool} with \texttt{TNat} is attempted, or when "occurs check" fails. We, however, do not check that the exact reason of failure is as expected for ill-typed programs, which is left for the future work.

Test cases with well-typed programs check that the inference algorithm successfully infers the type and it is correct. In order to check that the inferred type is correct, each well-typed program is accompanied with additional file that contains the expected type of a program. For a well-typed program, the test case passes only if \texttt{inferTypeClosed} successfully returns a type that matches the expected type. By "matches" we mean that two polymorphic types are alpha-equivalent, except that the order of \texttt{forall} binders does not matter; in other words, we assume that types $\forall \alpha. \forall \beta. \alpha \to \beta$ and $\forall \beta. \forall \alpha. \alpha \to \beta$ are equal. R\'emy calls this $\forall$-equality~\cite{Remy1992_SortedEqTheoryTypes}. Such assumption made it easier to add new test cases, as we do not need to think about implementation details when specifying expected types for the well-typed programs.

\subsection{Polytypes Equivalence}

It is worth mentioning how the comparison of an actual and expected types is implemented for a well-typed test case, which is not straightforward. We could use the \texttt{alphaEquiv} function provided by the free-foil package for this purpose. However, it does not distinguish the values of literals (as illustrated in Figure~\ref{fig:freefoil-alphaequiv-literals}), and therefore is not suitable for comparing types containing free variables. A possible workaround would be to generalize both types before performing a comparison, as this would replace all free type variables with the variables bound by "forall"s. But still, this will not work in our case (see Figure~\ref{fig:freefoil-alphaequiv-foralls}), since we do not want the order of 'forall's in the types to influence the comparison; i.e., we need $\forall$-equality. In short, free-foil's \texttt{alphaEquiv} does not fit our needs and another approach is necessary.

\begin{figure}[H]
  \begin{minted}[frame=single,fontsize=\small]{haskell}
>>> t1 = ("?a -> ?a" :: Type')
>>> t2 = ("?a -> ?b" :: Type')
>>> FreeFoil.alphaEquiv Foil.emptyScope t1 t2
True

>>> e1 = ("1" :: Exp')
>>> e2 = ("2" :: Exp')
>>> FreeFoil.alphaEquiv Foil.emptyScope e1 e2
True
  \end{minted}
  \caption[\texttt{FreeFoil.alphaEquiv} usage examples (1)]{Examples demonstrating that \texttt{FreeFoil.alphaEquiv} does not distinguish between different variable names or literal values.}
  \label{fig:freefoil-alphaequiv-literals}
\end{figure}

\begin{figure}[H]
  \begin{minted}[frame=single,fontsize=\small]{haskell}
>>> t1 = ("forall a. (forall b. a -> b)" :: Type')
>>> t2 = ("forall x. (forall y. x -> y)" :: Type')
>>> t3 = ("forall b. (forall a. a -> b)" :: Type')
>>> FreeFoil.alphaEquiv Foil.emptyScope t1 t2
True
>>> FreeFoil.alphaEquiv Foil.emptyScope t1 t3
False
  \end{minted}
  \caption[\texttt{FreeFoil.alphaEquiv} usage examples (2)]{Examples demonstrating that \texttt{FreeFoil.alphaEquiv} considers types with different variable names as equivalent, but is sensitive to the order of nested \texttt{forall}s.}
  \label{fig:freefoil-alphaequiv-foralls}
\end{figure}

Formally, we consider two polymorphic types equal (see Figure~\ref{fig:equiv-poly-rule}) if there exists a substitution of their bound type variables such that, after applying this substitution, the two types become syntactically identical i.e., alpha-equivalent, regardless of the order in which the "forall" quantifiers appear. In the implementation, we compare two polymorphic types using the \texttt{equivPoly} function (see Figure~\ref{fig:equiv-poly-impl}), which works as follows.

\begin{figure}[H]
  \begin{prooftree*}
    \hypo{T_2 = [x_1 \mapsto y_{i_1}, \ldots, x_n \mapsto y_{i_n}] T_1}
    \hypo{\{y_{i_1}, \ldots, y_{i_n}\} = \{y_1, \ldots, y_n\}}
    \infer2{ \forall \{x_1, \ldots, x_n\}. T_1 =_{\forall} \forall \{y_1, \ldots, y_n\}. T_2}
  \end{prooftree*}
  \caption{$\forall$-equivalence of polytypes}
  \label{fig:equiv-poly-rule}
\end{figure}

\begin{figure}[H]
  \begin{minted}[frame=single,fontsize=\small,linenos]{haskell}
equivPoly :: Type' -> Type' -> TypeInferencer n Bool
equivPoly l r = do
  (l', xs) <- specialize $ genAll l
  (r', ys) <- specialize $ genAll r
  if length xs /= length ys
    then return False
    else do
      levelMap <- gets tcLevelMap
      case unifyConstraint levelMap (Constraint (l', r')) of
        Left _ -> return False
        Right (Subst subst, _) -> do
          let matchings = [(x, y) | (x, TUVar y) <- Map.toList subst]
              allXs = List.sort xs == List.sort (map fst matchings)
              allYs = List.sort ys == List.sort (map snd matchings)
          return (allXs && allYs)
  where
    genAll t = generalizeWithIdents (Set.toList (freeVars t)) t
  \end{minted}
  \caption{\texttt{equivPoly} function for equivalence check of polytypes}
  \label{fig:equiv-poly-impl}
\end{figure}

First, we generalize both types for all present variables and immediately specialize them (lines 3 and 4). By doing this, we re-assign all existing free variables, if any, with the fresh ones, which is necessary to make sure all variables are present in the \texttt{tcLevelMap} in the \texttt{TypeInferencer} context, which is used later. Also, specialized types now can only have free type variables i.e., no \texttt{forall}s will be present in the types. Moreover, \texttt{xs} and \texttt{ys} returned by \texttt{specialize} contain all type variables in the types, since they were generalized before, so at this step we may already conclude that \texttt{l} and \texttt{r} are not equal, if the number of unique type variables differs, that is \texttt{length xs /= length ys} (lines 5 and 6). If it is not the case, we proceed further.

Next, we actually need to perform the alpha equivalence check. However, as was mentioned earlier, \texttt{FreeFoil.alphaEquiv} will not work correctly with the free variables. We solve the problem by using the already mentioned unification algorithm in order to unify a single constraint, in other words, we solve a single equation of form $l = r$ to get a substitution \texttt{subst}.

Finally, after obtaining the substitution, we check that it matches all type variables from the left type to those in the right type, and that all variables are accounted for, ensuring a one-to-one correspondence between variables in both types. If the substitution is not found, or type variables in \texttt{l} do not correspond to type variables in \texttt{r}, we conclude that types are not equal. The reason why \texttt{equivPoly} works in the \texttt{TypeInferencer} context and uses the \texttt{tcLevelMap} is only because \texttt{specialize} and \texttt{unifyConstraint} need this, which, could be avoided, but kept for the sake of simplicity of the implementation.
